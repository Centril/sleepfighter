apply plugin: 'android'
apply plugin: 'idea'

// Config:
def getVersionCode() { 8 }
def getMinSdk() { 9 }
def getTargetSdk() { 19 }

// Generate BuildTimestamp.java.
apply from: 'timestamp.gradle'

// Add all dependencies.
apply from: 'dependencies.gradle'

// Apply signing configuration.
apply from: 'signing.gradle'

android {
	buildToolsVersion "19.0.1"

	// Override manifest stuff.
    compileSdkVersion getTargetSdk()
    defaultConfig {
        minSdkVersion getMinSdk()
        targetSdkVersion getTargetSdk()
	    versionCode getVersionCode()
    }

	// Using Java 7, but no try-with-resources.
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_7
		targetCompatibility JavaVersion.VERSION_1_7
	}

	// Proguard settings:
    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

	// Introduce "releaseDebug" extending release but debuggable.
	buildTypes {
		debugRelease.initWith( buildTypes.release )
		debugRelease {
			debuggable      true
			signingConfig  signingConfigs.debug
		}
	}
	sourceSets.debugRelease.setRoot( 'src/release' )

	// Suffix all build types with type except for release, allowing parallel installs.
	buildTypes.each{ type ->
		type.packageNameSuffix = type.name == 'release' ? '' : ".$type.name"
	}
}

// Fix META-INF stuff.
apply from: 'metainf-fix.gradle'